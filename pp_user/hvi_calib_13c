;hvi_calib_13c
;
; FROM 900
; based on calib_13c
;	HvI, Apr 2011
;	changed calibration pulse to be 90 in z-filter period
;	to remove off-res dependent phase problems and have
;	pure amplitude modulation of signal
;
; USAGE: 	- 90 degree is no signal!
;		- 0 degree is full signal! 
;
;$CLASS=HighRes
;$DIM=1D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

; DEFINE PARAMETERS:

"p2=p1*2"
"p4=p3*2"
"d11=30m"
"d13=20u"
"d26=1s/(cnst4*4)"
"DELTA=p16+d16"
"DELTA1=d26-p16-d13-4u-d19*10-4.77*p1"

; START PULSE SEQUENCE

1 ze
    d11 pl16:f2
2   d1 do:f2
    3u pl1:f1
3 (p1 ph10)					; forward INEPT
    d26 pl2:f2
  (center (p2 ph10) (p4 ph10):f2 )
    d26 UNBLKGRAD
  (p1 ph11)					; back to z and do calibration
    4u
    d13 fq=cnst21 (bf ppm):f2			; switch offset
    d13 pl0:f2					; switch power
    p16:gp1*0.7					; make sure to start from z, removes off-res problems
    d16						; to large extent
  (p0 ph10):f2					; calibration pulse
    4u
    p16:gp1					; cleanup gradient 1
    d13 pl2:f2					; switch back power/offset
    d13 fq=0:f2
    d16
  (p3 ph13):f2					; regular back transfer
    50u
  (p4 ph10):f2
    50u
  (ralign (p1 ph10) (p3 ph14):f2 )		; reverse INEPT w/ 3-9-19 watergate
    d13
    p16:gp2
    DELTA1
    4u
  (center (p1*0.231 ph20 d19*2 p1*0.692 ph20 d19*2 p1*1.462 ph20 d19*2 p1*1.462 ph21 d19*2 p1*0.692 ph21 d19*2 p1*0.231 ph21):f1 (p4 ph10):f2 )

    d13
    p16:gp2
    DELTA1 pl16:f2
    4u BLKGRAD
  go=2 ph31 cpd2:f2
    d1 do:f2 wr #0
exit
  
ph10=0 				; fixed x-phase
ph11=1				; fixed y-phase
ph13=0 2			; first 90 13C
ph14=0 0 2 2			; second 90 13C
ph20=0				; watergate phase 1
ph21=2				; watergate phase 2
ph31=0 2 2 0	; receiver phase
  

;pl1 : 1H high power
;p1  : 1H high power 90
;p2  : 1H high power 180
;pl2 : 13C high power
;p3  : 13C high power 90
;p4  : 13C high power 180
;pl0 : 13C calibration pulse power
;p0  : 13C calibration pulse 0/90
;pl16: 13C decoupling power for 90=pcpd2
;cpd2: 13C decoupling GARP
;pcpd2: 13C decoupling 90 ~150us
;cpdprg2 : GARP decoupling

;cnst21: offset calibration pulse
;cnst22: offset 13C (~39ppm)
;cnst4: = J(CH) tune to signal of interest
;d26 : 1/(4J(CH)) via cnst4

;p16 : PFG gradient pulse  [1 msec]
;d16 : delay for gradient recovery
;d19 : ~ 1-2*DW (3-9-19 watergate delay)

;d1  : relaxation delay
;d11 : delay for disk I/O    [30 msec]
;d13 : short delay    [20 usec]

;for z-only gradients:
;gpz1: 50% cleanup z-filter
;gpz2: 20% cleanup final INEPT	

;use gradient files:   
;gpnam1: SINE.100
;gpnam2: SINE.100



;$Id: hsqcetf2gp,v 1.4 2007/04/11 13:34:30 ber Exp $
