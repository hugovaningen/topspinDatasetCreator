;hvi_hmqcetgp
; modified to make getprosol compatible
;
;RamCOOP paper
;A. Bax, R.H. Griffey & B.L. Hawkins, J. Magn. Reson. 55, 301 (1983)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>
#include <Grad.incl>


"p2=p1*2"
; 15N 180 pix shape Veglia
; needs high power
"p40=p21*7.093"
"spnam40='pix_rf0.00_7.093p1_bw0.80B1.mrf'"
"spw40=plw3"
"spoffs40=0"
"spoal40=0.5"

; 13C 180 z2iz shape Veglia 
; needs high power
"p41=p3*10.021"     
"spnam41='z2iz_rf0.00_10.021p1_bw2.40B1.mrf'"
"spoffs41=0.0"     
"spoal41=0.5"
"spw41=plw2"     
; this is 180 invert C
; based of p3, if 12us is 20.833 kH, length is 120.2
; looks very much like BIP pulse
; perfect inversion over 50 kHz = 200 ppm!
; by double application rephases evolution
; better performance than BIP900: less artifact in 13C dim (stripes )

; 13C 90 RamCOOP Glaser/Sattler
; needs 20 kHz power is 12.5us 90
"p42=30"
"spnam42='Ram-COOP1_100.300'"
"spnam43='Ram-COOP2_100.300'"
"spw42=plw2*pow(p3/13,2)"
"spw43=plw2*pow(p3/13,2)"
"spoffs42=0"
"spoffs43=0"
"spoal42=0"
"spoal43=0"

; 13C Crp adiabatic decoupling bi_p5m4sp_4sp.2
; Q =3 
; from getprosol
"spoffs31=0"
"spoal31=0.5"
"spoffs14=0"
"spoal14=0.5"
; decouple offset maintained at 60ppm, independent of o2p
"cnst12=60"

; fix gradient coherence selection
"gpnam1='SMSQ10.100'"
"gpnam2='SMSQ10.100'"
"gpz1=80"
"gpz2=40.2"
"p16=500"

; delays
"d2=1s/(cnst4*2)"
"d11=30m"
"d13=4u"

"in0=inf1/2"
"d0=10u"

"DELTA1=d2-p42-p1*2/PI"					;balance checmical shift evolution 1H
"DELTA2=d2-p42-p16-d16-8u-de"
"DELTA3=p16+d16-p42*0.529"
"DELTA4=p16+d16-d0-p40/2"
"DELTA5=d16-p42*0.529-4u"
"DELTA6=d16-d0-p40/2"


"acqt0=0"
baseopt_echo



1 ze 
2 d11 do:f2
  50u UNBLKGRAD
  50u pl9:f1
  d1 cw:f1 ph29
  50u do:f1
  50u fq=0:f2
  4u pl1:f1
  4u pl0:f2
3 (p1 ph1):f1
    DELTA1
  (p42:sp42 ph3):f2				;13C exite Ram_COOP
    DELTA3 									; conmpensate phase evolution
  (p41:sp41 ph5):f2				; 13C BIP rephase
    DELTA4
    d0
 (center (p2 ph2):f1 (p40:sp40 ph1):f3)	; ph2 invert phase HC MQ
    d0
   p16:gp1*EA 
    DELTA6 
  (p41:sp41 ph15):f2
    4u
   p16:gp1*-1*EA 
    DELTA5
  (p42:sp43 ph4):f2
    4u
   p16:gp2
    d16 pl12:f2
    DELTA2  BLKGRAD
    4u fq=cnst12(bf ppm):f2
  go=2 ph31 cpds2:f2 
  d11 do:f2 mc #0 to 2 F1EA(calgrad(EA), caldel(d0, +in0) & calph(ph3, +180) & calph(ph31, +180))
exit 
  

ph1=0                       ; 1H excite / 15N 180
ph2=0 0 0 0  2 2 2 2        ; 1H/15N 180 in t1
ph3=0 2                     ; excite 13C at start t1
ph4=0 0 0 0  2 2 2 2        ; excite 13C at end t1             
ph5=0 0 1 1  2 2 3 3	      ; 13C 180 in t1 first
ph15=2 2 1 1 0 0 3 3        ; 13C 180 in t1 second
ph29=0
ph31=0 2 0 2 2 0 2 0

;pl0 : 0W
;pl1 : 1H high power
;pl2 : 13C high power
;pl9 : 1H water presat 50-60 dB
;pl12: 13C decoupling power for Crp
;p1 : 1H hard 90
;p2 : 1H hard 180
;p3 : 13C hard 90
;p16: PFG 300-500us
;p42 : 13C Ram COOP pulse at 20 kHz
;p41 : 13C 180 z2iz
;p40 : 15N 180 pix 
;p63 : 1500 us for Crp54,1.5.20
;sp41 : 13C z2iz_rf0.00_10.021p1_bw2.40B1.mrf;
;sp42 : 13C Ram-COOP1_100.300
;sp43 : 13C Ram-COOP2_100.300
;sp40 : 15N 180 pix at high power
;d0 : incremented delay (2D)                  [3 usec]
;d1 : relaxation delay; 1-5 * T1
;d2 : 1/(2J)XH
;d11: delay for disk I/O                             [30 msec]
;d13: short delay                                    [4 usec]
;d16: PFG recovery delay 200us
;cnst4: = J(XH) 140Hz
;cnst12: 60ppm decoupling offset
;inf1: 1/SW(X) = 2 * DW(X)
;in0:  1/2SW(X) =  DW(X)
;nd0: 2
;ns: 8 * n
;ds: 128 (or 3min)
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpdprg2: 13C Crp decoupling bi_p5m4sp_4sp.2
;spnam14: Crp54.0.75,20.2
;spnam31: Crp54.1.5,20.2
;gpz1: 80%
;gpz2: 40.2%
;gpnam1 : SMSQ10.100
;gpnam2 : SMSQ10.100



;$Id:$
